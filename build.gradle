/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// Main gradle build file for Spring Social Facebook
// @author Chris Beams
// @author Craig Walls
// -----------------------------------------------------------------------------

// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
description = 'Spring Social Facebook'
abbreviation = 'SOCIAL-FACEBOOK'

apply plugin: 'base'
apply plugin: 'idea'

// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
//
// @see settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.social'

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        mavenRepo urls: 'http://maven.springframework.org/release'
        mavenRepo urls: 'http://maven.springframework.org/milestone'
        mavenRepo urls: 'http://maven.springframework.org/snapshot'
        mavenCentral()
    }
}


// -----------------------------------------------------------------------------
// Create collections of subprojects - each will receive their own configuration
// - all subprojects that start with spring-social-* are 'java projects'
// - documentation-related subprojects are not collected here
//
// @see configure(*) sections below
// -----------------------------------------------------------------------------

javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-social-')
}

// -----------------------------------------------------------------------------
// Configuration for all java subprojects
// -----------------------------------------------------------------------------
configure(javaprojects) {

    apply plugin: 'java' // tasks for conventional java lifecycle
    apply plugin: 'maven' // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse' // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'idea' // `gradle idea` to generate .ipr/.iml

    // set up dedicated directories for jars and source jars.
    // this makes it easier when putting together the distribution
    libsBinDir = new File(libsDir, 'bin')
    libsSrcDir = new File(libsDir, 'src')

    springSocialVersion = '1.0.2.BUILD-SNAPSHOT'
    jacksonVersion = '1.9.3'
    jspApiVersion = '2.1'
    junitVersion = '4.10'
    mockitoVersion = '1.9.0'
    servletApiVersion = '2.5'
    springVersion = '3.1.0.RELEASE'
    springTestMvcVersion = "1.0.0.BUILD-SNAPSHOT"

    sourceSets {
        main {
            resources {
                srcDirs = ['src/main/java']
            }
        }
        test {
            resources {
                srcDirs = ['src/test/java']
            }
        }
    }

    // dependencies that are common across all java projects
    dependencies {
        testCompile "junit:junit-dep:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    // enable all compiler warnings (GRADLE-1077)
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    // generate .classpath files without GRADLE_CACHE variable (GRADLE-1079)
    eclipseClasspath.variables = [:]
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// @see configure(javaprojects) above for general config
// -----------------------------------------------------------------------------

project('spring-social-facebook') {
    description = 'Facebook API'
    dependencies {
        compile "org.springframework.social:spring-social-core:$springSocialVersion"
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        testCompile "org.springframework:spring-test-mvc:$springTestMvcVersion"
    }
}

project('spring-social-facebook-web') {
    description = 'Facebook Web'
    dependencies {
        compile ("org.springframework:spring-web:$springVersion")
        compile ("javax.servlet:servlet-api:$servletApiVersion") { provided = true }
        compile ("javax.servlet.jsp:jsp-api:$jspApiVersion") { provided = true }
    }
}
